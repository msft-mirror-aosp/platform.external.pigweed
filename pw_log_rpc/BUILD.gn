# Copyright 2020 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")

import("$dir_pw_build/target_types.gni")
import("$dir_pw_docgen/docs.gni")
import("$dir_pw_protobuf_compiler/proto.gni")
import("$dir_pw_unit_test/test.gni")

config("default_config") {
  include_dirs = [ "public" ]
  visibility = [ ":*" ]
}

pw_source_set("logs") {
  public_configs = [ ":default_config" ]
  public = [ "public/pw_log_rpc/logs_rpc.h" ]
  sources = [ "logs_rpc.cc" ]
  public_deps = [
    ":log_queue",
    ":protos.pwpb",
    ":protos.raw_rpc",
  ]
}

pw_source_set("log_queue") {
  public_configs = [ ":default_config" ]
  public = [ "public/pw_log_rpc/log_queue.h" ]
  public_deps = [
    "$dir_pw_bytes",
    "$dir_pw_log",
    "$dir_pw_result",
    "$dir_pw_ring_buffer",
    "$dir_pw_status",
  ]
  sources = [ "log_queue.cc" ]
  deps = [ ":protos.pwpb" ]
}

pw_test("logs_rpc_test") {
  deps = [
    ":logs",
    "$dir_pw_rpc/raw:test_method_context",
  ]
  sources = [ "logs_rpc_test.cc" ]
}

pw_proto_library("protos") {
  sources = [ "pw_log_proto/log.proto" ]
}

pw_doc_group("docs") {
  sources = [ "docs.rst" ]
}

pw_test("log_queue_test") {
  sources = [ "log_queue_test.cc" ]
  deps = [
    ":log_queue",
    ":protos.pwpb",
    dir_pw_protobuf,
  ]
}

pw_test_group("tests") {
  tests = [
    ":log_queue_test",
    ":logs_rpc_test",
  ]
}
